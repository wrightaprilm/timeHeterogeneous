################################################################################
#
# RevBayes Example: Total-evidence dating under the fossilized birth-death model
#
# This file: Runs the full MCMC ...
#
# authors: Tracy A. Heath, Walker C. Pett, April M. Wright
#
################################################################################

#######################
# Reading in the data #
#######################
seed(1234)
# Create the full list of taxa (including all fossils and extant species #
taxa <- readTaxonData("data/final_tip_ages.tsv")

filenames <- v("data/Mol/18s.aln.nex", "data/Mol/28s.aln.nex", "data/Mol/ABD.aln.nex",
"data/Mol/Arg.aln.nex", "data/Mol/CAD.aln.nex", "data/Mol/EF1.aln.nex", "data/Mol/EF2.aln.nex",
"data/Mol/lg.aln.nex")

n_data_subsets <- filenames.size()
for (i in 1:n_data_subsets) {
   data[i] = readDiscreteCharacterData(filenames[i])
}

# Import the morphological character matrix #
# this file contains only the taxa for which morphological characters are available #
morpho <- readDiscreteCharacterData("data/Morph/AntMegaMatrixMinusAmbig.nex")

# Add the missing taxa to each data partition #
for (i in 1:n_data_subsets) {
    data[i].addMissingTaxa( taxa )
}

morpho.addMissingTaxa( taxa )

## helpers
mvi = 1
n_taxa <- taxa.size()

# Load the model files

source("scripts/model_FBDP_TEFBD.Rev") # FBD tree prior
source("scripts/model_UExp_TEFBD.Rev") # UExp relaxed clock

source("scripts/model_GTRG_TEFBD.Rev") # Molecular substitution model (GTR+G)

source("scripts/model_Morph_TEFBD.Rev") # Morphological character change model


########
# MCMC #
########

# initialize the model object #
mymodel = model(alpha)

mni = 1

# Create a vector of monitors #
# 1. for the full model #
monitors[mni++] = mnModel(filename="output/ants.log", printgen=10, lambda)
monitors[mni++] = mnStochasticVariable(filename="output/mcmc-stoch.log", printgen=10)

# 2. the tree #
# monitors to print RevGagets input
monitors[mni++] = mnFile(filename="output/model1_speciation_rates.log",lambda,printgen=10)
monitors[mni++] = mnFile(filename="output/model1_speciation_times.log",timeline,printgen=10)
monitors[mni++] = mnFile(filename="output/model1_extinction_rates.log",mu,printgen=10)
monitors[mni++] = mnFile(filename="output/model1_extinction_times.log",timeline,printgen=10)
monitors[mni++] = mnFile(filename="output/model1_sampling_rates.log",psi,printgen=10)
monitors[mni++] = mnFile(filename="output/model1_sampling_times.log",timeline,printgen=10)
# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves)
#trace = readStochasticVariableTrace(file="output/mcmc-stoch.log")
#mymcmc.initializeFromTrace(trace)
# Run the MCMC #
mymcmc.run(generations=50000)

# Quit RevBayes #
q()
